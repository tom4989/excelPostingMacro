VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "clsブック制御"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' *********************************************************************************************************************
' 定数
' *********************************************************************************************************************
'

' *********************************************************************************************************************
' 変数
' *********************************************************************************************************************
'
Private wb制御対象のブック As Workbook

Private flgブック開いてた As Boolean

Public Property Get wbブック() As Workbook
    Set wbブック = wb制御対象のブック
End Property


' *********************************************************************************************************************
' 定数(Public) ※ クラス内では定数をPublicにできないため、Propertyを利用
' *********************************************************************************************************************
'

' *********************************************************************************************************************
' 機能：コンストラクタ ※ 引数は指定できない
' *********************************************************************************************************************
'
Public Sub Class_Initialize()

    Set wb制御対象のブック = Nothing

End Sub

' *********************************************************************************************************************
' 機能：ブックを開く。ブックが既に開いている場合、状態を記憶する。
' *********************************************************************************************************************
'
Sub openブック(txtファイルパス As String)

    Dim objFSO As Object
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    
    Dim txtファイル名 As String
    txtファイル名 = objFSO.GetFileName(txtファイルパス)
    
    Dim wb処理中のブック As Workbook
    
    For Each wb処理中のブック In Workbooks
    
        If wb処理中のブック.Name = txtファイル名 Then
        
            Set wb制御対象のブック = wb処理中のブック
            flgブック開いてた = True
            
            Exit Sub
        End If
    Next wb処理中のブック
    
    Set wb制御対象のブック = Workbooks.Open(txtファイルパス, UpdateLinks:=0, IgnoreReadOnlyRecommended:=False)
    flgブック開いてた = False

End Sub

' *********************************************************************************************************************
' 機能：このクラスでブックを開いていた場合、ブックを閉じる。
' *********************************************************************************************************************
'
Sub closeブック()

    If Not flgブック開いてた Then
        wb制御対象のブック.Close
    End If
    
    Set wb制御対象のブック = Nothing
    
End Sub
