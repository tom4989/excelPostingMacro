VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "cls転記元シート"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

' *********************************************************************************************************************
' 定数
' *********************************************************************************************************************
'
Private Const KEY_転記元情報 = "転記元情報"
Private Const KEY_ファイルパス = "ファイルパス"
Private Const KEY_シート名 = "シート名"
Private Const KEY_列情報 = "列情報"
Private Const KEY_キー列 = "キー列"

' *********************************************************************************************************************
' 変数
' *********************************************************************************************************************
'
Private txtファイルパス As String
Private txtシート名 As String
Private dic列情報 As Object

Private varシート内容 As Variant

' *********************************************************************************************************************
' 定数(Public) ※ クラス内では定数をPublicにできないため、Propertyを利用
' *********************************************************************************************************************
'

' *********************************************************************************************************************
' 機能：コンストラクタ ※ 引数は指定できない
' *********************************************************************************************************************
'
Public Sub Class_Initialize()

End Sub

' *********************************************************************************************************************
' 機能：初期化
' *********************************************************************************************************************
'
Public Function 初期化(obj設定値シート As cls設定値シート)

    txtファイルパス = _
        obj設定値シート.設定値リスト.Item(KEY_転記元情報).Item(KEY_ファイルパス)
        
    txtシート名 = _
        obj設定値シート.設定値リスト.Item(KEY_転記元情報).Item(KEY_シート名)

    Set dic列情報 = _
        obj設定値シート.設定値リスト.Item(KEY_転記元情報).Item(KEY_列情報)

End Function

' *********************************************************************************************************************
' 機能：転記元ブックの転記元シートの内容をVariant変数に格納する
' *********************************************************************************************************************
'
Public Sub シート読込()

    Dim objブック制御 As New clsブック制御
    objブック制御.openブック (txtファイルパス)
    
    Dim ws転記元 As Worksheet
    Set ws転記元 = objブック制御.wbブック.Sheets(txtシート名)

    Dim lng最終行, lng最終列 As Long
    lng最終行 = 最終行取得(ws転記元)
    lng最終列 = 最終列取得(ws転記元)

    With ws転記元
        varシート内容 = .Range(.Cells(1, 1), .Cells(lng最終行, lng最終列))
    End With

    objブック制御.closeブック

End Sub

' *********************************************************************************************************************
' 機能：パラメータで指定された名前と一致する行の情報を Dictionary オブジェクトで返却する。
'       形式は下記の通り。
'
'       Dictionary(
'           (列名, 列の値),
'           (列名, 列の値),
'           ...
'       )
' *********************************************************************************************************************
'
Public Function 転記元情報取得(txt名前 As String) As Object

    Dim dic転記元行 As Object
    Set dic転記元行 = Nothing
    
    Dim i, lngキー列 As Long
    
    lngキー列 = CAlp2Num(dic列情報.Item(KEY_キー列))
    
    For i = LBound(varシート内容) To UBound(varシート内容)

        If txt名前 = varシート内容(i, lngキー列) Then
        
            Set dic転記元行 = CreateObject("Scripting.Dictionary")
            
            Dim txt列名 As Variant
            
            For Each txt列名 In dic列情報
            
                Call dic転記元行.Add(txt列名, varシート内容(i, CAlp2Num(dic列情報(txt列名))))
            
            Next txt列名
            
            Set 転記元情報取得 = dic転記元行
            Exit Function
            
        End If
    Next
    
    Set 転記元情報取得 = dic転記元行
    
End Function
